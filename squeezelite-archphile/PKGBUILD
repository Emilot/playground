# Maintainer: Tux <info@archphile.org>
# Website: http://archphile.org
# Based on mpd-light from AUR made by the following people:
#  (c) Adrian Smith 2012-2015, triode1@btinternet.com
#      Ralph Irving 2015-2016, ralph_irving@hotmail.com

pkgname=squeezelite-archphile
pkgver=v1.8.5_809.637e941
pkgrel=1
pkgdesc='Lightweight headless squeezebox emulator'
arch=('armv7h' 'aarch64')
url='https://github.com/ralph-irving/squeezelite'
license=(GPL3)
makedepends=('ffmpeg')
depends=('alsa-lib' 'faad2' 'flac' 'libmad' 'libvorbis' 'mpg123' 'libsoxr')
provides=('squeezelite')
conflicts=('squeezelite')
source=('git+https://github.com/ralph-irving/squeezelite'
	'service'
	'conffile'
	'install'
	)

install=install

pkgver() {
	cd squeezelite

	# printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	printf "%s.%s" "$(cat squeezelite.h|grep VERSION | awk '{print $3}' | sed 's/\"//g;s/-/_/g')" "$(git rev-parse --short HEAD)"
}

build() {
	cd squeezelite

	export LDFLAGS="${LDFLAGS} -lasound -lpthread -lm -lrt"
	export CFLAGS="${CFLAGS} -DDSD -DRESAMPLE -DVISEXPORT -DFFMPEG"
	export OPTS="${OPTS} -DIR"
	make
}

package() {
	cd squeezelite

	# Main binary
	install -m0755 -D squeezelite "${pkgdir}/usr/bin/squeezelite"
	install -Dm644 ../conffile "${pkgdir}/etc/squeezelite.conf.default"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/squeezelite.service"
	install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/squeezelite.LICENSE
}


sha256sums=('SKIP'
            '3a1c7762f08cabf82929de1862622c836cf644c5733de7834b02dd9562e76a45'
            'f0753a1cbd0194119226587ff9c12257438674d9b8e0179d22f0d5461ad3a70a'
            '2c52ea3b2916f9d67e3029506eabba2dabb3193c2833bcc7d2ff268d3a9ab5b9')

