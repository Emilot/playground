# Maintainer: Tux <info@archphile.org>
# Website: http://archphile.org
# Based on squeezelite-git from AUR made by the following people:
# IanDury

pkgname=squeezelite-archphile
pkgver=v1.8.6_825.1b96b62
pkgrel=1
pkgdesc='Lightweight headless squeezebox emulator with Native DSD patch'
arch=('armv7h' 'aarch64')
url='https://github.com/ralph-irving/squeezelite'
license=(GPL3)
makedepends=('ffmpeg-archphile')
depends=('alsa-lib' 'faad2' 'flac' 'libmad' 'libvorbis' 'mpg123' 'libsoxr')
provides=('squeezelite')
conflicts=('squeezelite')
source=('git+https://github.com/ralph-irving/squeezelite'
	'service'
	'conffile'
	'install'
	)

install=install

pkgver() {
	cd squeezelite

	# printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	printf "%s.%s" "$(cat squeezelite.h|grep VERSION | awk '{print $3}' | sed 's/\"//g;s/-/_/g')" "$(git rev-parse --short HEAD)"
}

build() {
	cd squeezelite
	cat ./scripts/squeezelite-ralphy-dsd.patch | patch -p 0
	export LDFLAGS="${LDFLAGS} -lasound -lpthread -lm -lrt"
	export CFLAGS="${CFLAGS} -DDSD -DRESAMPLE -DVISEXPORT -DFFMPEG"
	export OPTS="${OPTS} -DIR"
	make
}

package() {
	cd squeezelite

	# Main binary
	install -m0755 -D squeezelite "${pkgdir}/usr/bin/squeezelite"
	install -Dm644 ../conffile "${pkgdir}/etc/squeezelite.conf"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/squeezelite.service"
	install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/squeezelite.LICENSE
}


sha256sums=('SKIP'
            '15695d9dbb94e41f7aae7450dcfa9fbba83f25f4d8f2427e872c20c302e486cc'
            'f3595f1953dfc1864a0f247478562341bfc2cb100e4640ba6016e9689fde37734'
            '2c52ea3b2916f9d67e3029506eabba2dabb3193c2833bcc7d2ff268d3a9ab5b9')

