# Maintainer: Tux <info@archphile.org>
# Website: http://archphile.org
# Based on mpd-minimal from AUR made by the following people:
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani at gmail dot com>

pkgname=mpd-archphile-minimal
_pkgname=mpd
pkgver=0.19.18
pkgrel=1
pkgdesc='Flexible, powerful, server-side application for playing music. Minimal version optimized for Archphile distribution.'
url='http://www.musicpd.org/'
license=('GPL')
arch=('arm' 'armv6h' 'armv7h' 'aarch64')
depends=('alsa-lib' 'libsndfile' 'flac' 'icu' 'libvorbis' 'libmad' 'curl' 'faad2' 'sqlite' 'libmms' 'libid3tag' 'libmpdclient')
makedepends=('boost' 'doxygen')
provides=("mpd=$pkgver")
conflicts=('mpd' 'mpd-archphile-sacd' 'mpd-archphile-native-dsd')
replaces=('mpd')
source=("http://www.musicpd.org/download/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz"
        'tmpfiles.d'
	'mpd.conf')
sha1sums=('41065043589208db1100c5a3f3c15fbd3ac87669'
          'f4d5922abb69abb739542d8e93f4dfd748acdad7'
          '03523cd3334133ea44af00d1e8cd8e628854a818')
backup=('etc/mpd.conf')
install=install

prepare() {
	# Temporary; see FS#48372
	install -d "${srcdir}"/pkg-config
	ln -s /usr/lib/pkgconfig/libsystemd.pc "${srcdir}"/pkg-config/libsystemd-daemon.pc
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--enable-pipe \
	--enable-libmpdclient \
	--enable-alsa \
	--enable-mad \
	--enable-aac \
	--enable-vorbis \
	--enable-curl \
	--enable-mms \
	--enable-id3 \
	--enable-tcp \
	--enable-largefile \
	--enable-sndfile \
	--disable-libwrap \
	--disable-ao \
    	--disable-httpd-output \
    	--disable-jack \
    	--disable-openal \
    	--disable-oss \
    	--disable-pulse \
    	--disable-recorder-output \
    	--disable-roar \
    	--disable-shout \
    	--disable-adplug \
    	--disable-audiofile \
    	--disable-ffmpeg \
    	--disable-fluidsynth \
    	--disable-gme \
    	--disable-mikmod \
    	--disable-modplug \
    	--disable-mpg123 \
   	--disable-opus \
    	--disable-sidplay \
    	--disable-wavpack \
    	--disable-wildmidi \
    	--disable-expat \
    	--disable-ipv6 \
    	--disable-nfs \
    	--disable-smbclient \
    	--disable-soundcloud \
   	--disable-upnp \
    	--disable-bzip2 \
    	--disable-iso9660 \
    	--disable-zlib \
    	--disable-zzip \
    	--disable-cdio-paranoia \
    	--disable-lsr \
    	--disable-soxr \
    	--disable-lame-encoder \
    	--disable-shine-encoder \
    	--disable-twolame-encoder \
    	--disable-vorbis-encoder \
    	--disable-wave-encoder \
    	--disable-neighbor-plugins \
    	--with-zeroconf=no \
    	--with-systemdsystemunitdir=/usr/lib/systemd/system

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../mpd.conf "${pkgdir}"/etc/mpd.conf
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf
	install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd{,/playlists}

	install -Dm644 "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
	sed '/\[Service\]/a User=mpd' -i "${pkgdir}"/usr/lib/systemd/system/mpd.service
	sed '/WantedBy=/c WantedBy=default.target' -i "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
    	sed '/ExecStart=/c ExecStart=/usr/bin/nice -n -16 /usr/bin/mpd --no-daemon' -i "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service

}
