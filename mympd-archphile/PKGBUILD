# Maintainer: Mike Andonov <info@archphile.org>
# Website: http://archphile.org
# Based on ympd-git from AUR made by the following people:
# Maintainer: Philippe Mongeau <ph.mongeau@gmail.com>

pkgname=mympd-archphile
_pkgname=ympd
pkgver=1.2.3.r55.g4295d15
pkgrel=2
pkgdesc="A standalone MPD Web GUI based on YMPD - Default port set to 80"
arch=('armv7h' 'aarch64')
url="http://github.org/jcorporation"
license=('BSD')
depends=('libmpdclient' 'openssl')
makedepends=('cmake' 'git')
optdepends=()
provides=("${_pkgname}")
conflicts=("${_pkgname}")
replaces=('ympd-archphile')
source=("git+https://github.com/jcorporation/ympd.git#commit=2c27cbc037f4c940cb007111e4afc99de887d159"
         "ympd.service")

sha256sums=('SKIP'
	    '9ed410694cf9c88c2fd5c9df177e912e09cfc151fe254d97925d635c1bc94a0f')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed -E 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"

 
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX:PATH=/usr -DWITH_DYNAMIC_ASSETS=ON -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd "${srcdir}/${_pkgname}/build"
  make DESTDIR="$pkgdir/" install
#  install -Dm644  "${srcdir}/${_pkgname}/contrib/mympd.service" "$pkgdir/usr/lib/systemd/system/mympd.service"
  install -Dm644  "../../ympd.service" "$pkgdir/usr/lib/systemd/system/ympd.service"
  install -Dm644  "${srcdir}/${_pkgname}/contrib/ympd.default" "$pkgdir/etc/default/ympd"
  sed '/WEB_PORT=8080/c WEB_PORT=80' -i "$pkgdir/etc/default/ympd"
}

