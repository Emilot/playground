#  Maintainer: Tux <info@archphile.org>

pkgname=mpd-archphile-sacd
_pkgname=mpd
pkgver=r10099.598258d
pkgrel=3
pkgdesc='Flexible, powerful, server-side application for playing music. Experimental version with support of SACD ISO playback'
url='http://www.musicpd.org/'
license=('GPL')
arch=('arm' 'armv6h' 'armv7h')
depends=('flac' 'audiofile' 'libsndfile' 'icu' 'libmad' 'curl' 'faad2' 'sqlite' 'libmms' 'libid3tag' 'libmpdclient' 'libsoxr' 'ffmpeg-archphile' 'avahi')
makedepends=('boost' 'doxygen' 'git')
provides=("mpd=$pkgver")
conflicts=('mpd' 'mpd-archphile' 'mpd-archphile-native-dsd')
source=('git+git://git.musicpd.org/manisiutkin/mpd.git'
        'tmpfiles.d'
	'mpd.conf')
sha1sums=('SKIP'
          'f4d5922abb69abb739542d8e93f4dfd748acdad7'
          'ec4adda56ab9341650e51b03e107de0734750b4f')
backup=('etc/mpd.conf')
install=install

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname

  ./autogen.sh

  ./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-libmpdclient \
		--enable-mad \
		--enable-soxr \
		--enable-ffmpeg \
                --enable-audiofile \
		--enable-sndfile \
		--enable-sacdiso \
		--enable-dvdaiso \
		--disable-ao \
		--disable-jack \
		--disable-modplug \
		--disable-pulse \
		--disable-shout \
		--disable-sidplay \
		--disable-soundcloud \
		--disable-wavpack \
		--disable-opus \
		--disable-vorbis \
		--disable-ipv6 \
		--disable-recorder-output \
		--disable-iso9660 \
		--disable-zzip \
		--disable-wildmidi \
		--disable-oss \
		--disable-fluidsynth \
		--disable-gme \
		--disable-bzip2 \
		--disable-lame-encoder \
	        --disable-shine-encoder \
                --disable-twolame-encoder \
                --disable-vorbis-encoder \
                --disable-wave-encoder \
                --disable-nfs \
                --disable-smbclient \
                --disable-httpd-output \
                --disable-oss \
        	--disable-mikmod \
		--disable-modplug \
        	--disable-mpg123 \
	        --disable-wildmidi \
                --disable-lsr \
		--with-zeroconf=avahi \
		--with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
	cd "${srcdir}/${_pkgname}"
	
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../mpd.conf "${pkgdir}"/etc/mpd.conf
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/mpd.conf
	install -d -g 45 -o 45 "${pkgdir}"/var/lib/mpd{,/playlists}

	install -Dm644 "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
	sed '/\[Service\]/a User=mpd' -i "${pkgdir}"/usr/lib/systemd/system/mpd.service
	sed '/WantedBy=/c WantedBy=default.target' -i "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
        sed '/ExecStart=/c ExecStart=/usr/bin/nice -n -16 /usr/bin/mpd --no-daemon' -i "${pkgdir}"/usr/lib/systemd/{system,user}/mpd.service
  find "$pkgdir" -type d -name '.git' -exec rm -r '{}' +
}
